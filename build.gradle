plugins {
    id 'java'
    id "com.gradleup.shadow" version "8.3.2"
}

dependencies {
    implementation project(":common")
    implementation project(":spigot")
    implementation project(":bungeecord")
    implementation project(":velocity")
}

allprojects {
    apply plugin: 'java'

    group = 'dev.siebrenvde'
    version = '2.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = "papermc"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
    }
}

subprojects {
    def targetJavaVersion = 21
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'

        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }
}

shadowJar {
    archiveClassifier.set("")

    // Relocate dependencies
    // relocate "net.kyori", "dev.siebrenvde.staffchat.dependencies.kyori" - Breaks Velocity
    relocate "com.electronwill.nightconfig", "dev.siebrenvde.staffchat.dependencies.nightconfig"
    relocate "org.incendo", "dev.siebrenvde.staffchat.dependencies.incendo"
    relocate "io.leangen.geantyref", "dev.siebrenvde.staffchat.dependencies.geantyref" // Dependency for cloud
}